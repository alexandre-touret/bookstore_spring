plugins {
    id 'org.springframework.boot' version "${springBootVersion}"
    id 'io.spring.dependency-management' version "${springDependencyManagementVersion}"
    id 'java'
    id 'idea'
}

bootJar {
    enabled = false
}

allprojects {
    apply plugin: 'java'

    group = 'info.touret.bookstore'
    version = '0.0.1-SNAPSHOT'


    repositories {
        mavenCentral()
        maven { url 'https://repo.spring.io/milestone' }
    }

    test {
        useJUnitPlatform()
    }

}

subprojects {

    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    sourceCompatibility = '17'

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }


    dependencies {

        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j'
        implementation 'io.micrometer:micrometer-registry-prometheus'
        implementation 'org.springframework.boot:spring-boot-starter-aop'
        implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'
        implementation 'com.fasterxml.jackson.core:jackson-annotations'
        implementation 'io.opentracing.contrib:opentracing-spring-jaeger-cloud-starter:3.3.1'
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    bootJar {
        enabled = true
    }


}
project(':rest-book') {
    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        runtimeOnly 'org.postgresql:postgresql'
        testImplementation 'com.h2database:h2'
    }
}

project(':rest-number') {
    dependencies {
        implementation ('com.github.javafaker:javafaker:1.0.2'){
            exclude group:'org.yaml', module:'snakeyaml'
        }
        implementation 'org.yaml:snakeyaml:2.2'
    }
}
